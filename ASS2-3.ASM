STACK 	SEGMENT	PARA	STACK
		DW		100H DUP(?)
STACK	ENDS

DATA	SEGMENT	PARA
	TABLE_LEN	EQU	9
	STR_LEN		EQU 7
	TABLE 	DB	'QIQI',20H,0,'$'
			DB 	'CHEN',20H,0,'$'
			DB	'XIAN',20H,0,'$'
			DB	'QIQJ',20H,0,'$'
			DB	'XHAN',20H,0,'$'
			DB	'XIBN',20H,0,'$'
			DB	'XHQI',20H,0,'$'
			DB	'LOVE',20H,0,'$'
			DB	'SURE',20H,0,'$'
	TEMP	DB	STR_LEN DUP(?)
	NEW_LINE	DB	0DH,0AH,'$'
DATA 	ENDS

CODE 	SEGMENT PARA
		ASSUME	CS:CODE,DS:DATA,SS:STACK

NEWLINE MACRO
	PUSH 	DX
	PUSH	AX
	MOV 	DX,OFFSET NEW_LINE
	MOV 	AH,9
	INT 	21H
	POP 	AX
	POP 	DX
	ENDM

OUTPUT 	PROC
	PART1:
		MOV 	CX,TABLE_LEN
		MOV 	SI,OFFSET TABLE
	LP1:	
		MOV 	DX,SI
		MOV 	AH,9
		INT 	21H				 
		ADD		SI,STR_LEN
		LOOP	LP1
		NEWLINE
		RET
OUTPUT	ENDP

COMP	PROC
	COMPARE:
		PUSH	SI
		PUSH 	CX
		CLD
		PUSH 	SI
		MOV 	CX,1
	SLP1:	
		LODSB
		CMP 	AL,00H
		JZ 		SLP1_1
		INC 	CX
		JMP 	SHORT SLP1
	SLP1_1:	
		POP 	SI
		REPE 	CMPSB
		JA 		SL1
		JB 		SL2
		MOV 	AL,1	;SI=DI
		JMP 	SHORT RETURN
	SL1:	
		MOV 	AL,2 	;SI>DI
		JMP  	SHORT RETURN
	SL2:		
		MOV 	AL,0	;SI<DI
	RETURN:	
		POP 	CX
		POP 	SI
		RET
COMP	ENDP

STRCPY 	PROC
	STRING_COPY:
		PUSH	SI
		PUSH 	DI
		PUSH 	AX
		CLD
	CPY_LP1:
		LODSB
		STOSB
		CMP 	AL,0
		JNZ		CPY_LP1
		POP 	AX
		POP 	DI
		POP		SI
		RET
STRCPY 	ENDP

EXCHG	PROC
	EXCHANGE:
		PUSH 	SI
		PUSH 	DI

		MOV 	DI,OFFSET TEMP
		CALL 	STRCPY

		MOV		DI,SI
		ADD 	SI,STR_LEN
		CALL 	STRCPY

		MOV 	DI,SI
		MOV 	SI,OFFSET TEMP
		CALL 	STRCPY
		POP 	DI
		POP 	SI
		RET
EXCHG	ENDP

SORT	PROC
	PART2:
		MOV		CX,TABLE_LEN
		DEC		CX
	LP2:
		MOV 	BX,1
		MOV 	SI,OFFSET TABLE
		PUSH	CX

	LP2_1:
		MOV 	AX,SI
		ADD 	AX,STR_LEN
		MOV		DI,AX
		CALL 	COMP
		CMP 	AL,1
		JBE		CONTINUE
		CALL 	EXCHG
		MOV		BX,0
	CONTINUE:
		ADD 	SI,STR_LEN
		LOOP 	LP2_1

		POP 	CX
		DEC 	CX
		CMP		BX,1
		JZ 		SORT_RETURN
		JMP		SHORT LP2
	SORT_RETURN:
		RET	
SORT 	ENDP

MAIN	PROC 	FAR
	MAIN_PART:
		MOV 	AX,DATA
		MOV 	DS,AX
		MOV 	ES,AX
		;DISPLAY ORIGIN TABLE
		CALL	OUTPUT
		;SORT
		CALL 	SORT
		;DISPLAY ORDERED TABLE
		CALL 	OUTPUT

	EXIT:	
		MOV 	AX,4C00H
		INT 	21H
MAIN 	ENDP

CODE 	ENDS
		END 	MAIN